!-------------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!------------------------------------------------------------------------------

!> @brief Tangent linear deriving exner from equation of state.
module tl_derive_exner_from_eos_alg_mod

  use constants_mod,                    only: i_def
  use field_mod,                        only: field_type
  use fs_continuity_mod,                only: W3
  use log_mod,                          only: log_event, &
                                              LOG_LEVEL_ERROR
  use derived_config_mod,               only: bundle_size
  use field_indices_mod,                only: igh_p, igh_t, igh_d, &
                                              igh_u, igh_w, igh_uv
  use formulation_config_mod,           only: eos_method,         &
                                              eos_method_sampled, &
                                              eos_method_projected
  use tl_project_eos_pressure_kernel_mod, &
                                        only: tl_project_eos_pressure_kernel_type
  use tl_sample_eos_pressure_kernel_mod, &
                                        only: tl_sample_eos_pressure_kernel_type

  implicit none

  private

  public :: tl_derive_exner_from_eos

contains

  !> @details The tangent linear for specifying exner from the equation of state
  !>
  !> @param[inout] state                Change in Prognostic model state
  !> @param[in]    moist_dyn_gas_law    Change in Gas law component of moist
  !>                                    dynamics factors
  !> @param[inout] ls_state             Lin state for Prognostic model state
  !> @param[in]    ls_moist_dyn_gas_law Lin state for Gas law component of
  !>                                    moist dynamics factors
  subroutine tl_derive_exner_from_eos( state,             &
                                       moist_dyn_gas_law, &
                                       ls_state,          &
                                       ls_moist_dyn_gas_law )

    use operator_mod,                   only: operator_type
    use sci_geometric_constants_mod,    only: get_coordinates, &
                                              get_panel_id
    use sci_fem_constants_mod,          only: get_inverse_mass_matrix_fe, &
                                              get_qr_fe
    use quadrature_xyoz_mod,            only: quadrature_xyoz_type

    implicit none

    type( field_type ), dimension(bundle_size), intent( inout ) :: state
    type( field_type ),                         intent( in )    :: moist_dyn_gas_law
    type( field_type ), dimension(bundle_size), intent( in )    :: ls_state
    type( field_type ),                         intent( in )    :: ls_moist_dyn_gas_law

    type( field_type ),           pointer :: chi(:) => null()
    type( field_type ),           pointer :: panel_id => null()
    type( operator_type ),        pointer :: m3_inv => null()
    type( quadrature_xyoz_type ), pointer :: qr => null()

    integer( kind=i_def ) :: mesh_id

    mesh_id = state(igh_p)%get_mesh_id()

    m3_inv  => get_inverse_mass_matrix_fe(W3, mesh_id)
    chi => get_coordinates(mesh_id)
    panel_id => get_panel_id(mesh_id)
    qr => get_qr_fe()

    select case(eos_method)
    case(eos_method_sampled)
      call invoke( name = 'sample_eos_pressure',                                   &
                   tl_sample_eos_pressure_kernel_type( state(igh_p),               &
                                                       state(igh_d),               &
                                                       state(igh_t),               &
                                                       moist_dyn_gas_law,          &
                                                       ls_state(igh_d),            &
                                                       ls_state(igh_t),            &
                                                       ls_moist_dyn_gas_law ) )
    case(eos_method_projected)
      call invoke( name = 'project_eos_pressure',                                   &
                   tl_project_eos_pressure_kernel_type( state(igh_p),               &
                                                        state(igh_d),               &
                                                        state(igh_t),               &
                                                        moist_dyn_gas_law,          &
                                                        ls_state(igh_d),            &
                                                        ls_state(igh_t),            &
                                                        ls_moist_dyn_gas_law,       &
                                                        chi,                        &
                                                        panel_id, m3_inv, qr ) )
    case default
      call log_event( "Gungho: Unrecognised method to calculate pressure", LOG_LEVEL_ERROR )
    end select

    nullify( m3_inv, chi,  &
             panel_id, qr )

  end subroutine tl_derive_exner_from_eos

end module tl_derive_exner_from_eos_alg_mod
