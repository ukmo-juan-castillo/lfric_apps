!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief An algorithm for performing 1D vertical semi-Lagrangian advective transport.
!> @details The algorithm performs a 1D vertical semi-Lagrangian advective
!!          transport of a field. It computes the field at the departure point
!!          using a cubic or quintic interpolation.

module vertical_sl_advective_alg_mod

  ! Constants and types
  use constants_mod,                   only: r_tran, i_def, l_def
  use fs_continuity_mod,               only: W3, Wtheta
  use integer_field_mod,               only: integer_field_type
  use log_mod,                         only: log_event, LOG_LEVEL_ERROR
  use mesh_mod,                        only: mesh_type
  use r_tran_field_mod,                only: r_tran_field_type
  use timing_mod,                      only: start_timing, stop_timing, &
                                             tik, LPROF

  ! Transport control
  use transport_controller_mod,        only: transport_controller_type
  use transport_counter_mod,           only: transport_counter_type
  use transport_enumerated_types_mod,  only: monotone_none,                    &
                                             monotone_strict,                  &
                                             monotone_relaxed,                 &
                                             vertical_monotone_order_constant, &
                                             vertical_monotone_order_linear,   &
                                             vertical_monotone_order_high
  use transport_metadata_mod,          only: transport_metadata_type
  use wind_precomputations_alg_mod,    only: wind_precomputations_type

  ! Algorithms and kernels
  use end_of_transport_step_alg_mod,   only: end_of_advective_step_alg
  use vertical_cubic_sl_kernel_mod,    only: vertical_cubic_sl_kernel_type
  use vertical_quintic_sl_kernel_mod,  only: vertical_quintic_sl_kernel_type

  ! Configs
  use transport_config_mod,            only: vertical_sl_order,                &
                                             vertical_sl_order_cubic,          &
                                             vertical_sl_order_quintic,        &
                                             vertical_sl_order_cubic_hermite,  &
                                             vertical_sl_order_linear

  implicit none

  private

  public :: vertical_sl_advective_alg

contains

  !-----------------------------------------------------------------------------
  !> @brief An algorithm to interpolate data to departure points (SL-advection).
  !!
  !> @param[in,out]  field_np1              Field at timestep n+1 = field_n at
  !!                                        departure point
  !> @param[in]      field_n                Field at timestep n
  !> @param[in,out]  transport_controller   Object containing metadata and
  !!                                        precomputations for controlling the
  !!                                        transport of the field
  subroutine vertical_sl_advective_alg( field_np1, field_n,                    &
                                        transport_controller )

    implicit none

    ! Arguments
    type(r_tran_field_type),         intent(inout) :: field_np1
    type(r_tran_field_type),         intent(in)    :: field_n
    type(transport_controller_type), intent(inout) :: transport_controller

    ! Mesh and transport objects
    type(mesh_type),                 pointer :: mesh
    type(wind_precomputations_type), pointer :: wind_precomputations
    type(transport_counter_type),    pointer :: transport_counter
    type(transport_metadata_type),   pointer :: transport_metadata
    integer(kind=i_def)                      :: monotone, monotone_order
    integer(kind=i_def)                      :: sl_order
    integer(kind=i_def)                      :: mesh_id
    integer(kind=i_def)                      :: space
    integer(kind=i_def)                      :: step
    integer(kind=i_def)                      :: splitting
    logical(kind=l_def)                      :: reversibility
    logical(kind=l_def)                      :: log_space
    integer(tik)                             :: id

    ! Interpolation coefficients
    type(r_tran_field_type),  pointer :: linear_coeffs(:)
    type(r_tran_field_type),  pointer :: interp_coeffs(:)
    type(integer_field_type), pointer :: interp_indices(:)

    if ( LPROF ) call start_timing( id, 'transport.sl_vertical' )

    if ( vertical_sl_order /= vertical_sl_order_cubic           .and. &
         vertical_sl_order /= vertical_sl_order_cubic_hermite   .and. &
         vertical_sl_order /= vertical_sl_order_quintic               ) then
      call log_event( "vertical_sl_advective: Invalid vertical_sl_order", LOG_LEVEL_ERROR )
    end if

    ! Get transport objects
    mesh => field_n%get_mesh()
    wind_precomputations => transport_controller%get_wind_precomputations()
    transport_counter => transport_controller%get_transport_counter()
    transport_metadata => transport_controller%get_transport_metadata()

    mesh_id = mesh%get_id()
    space = field_n%which_function_space()
    monotone = transport_metadata%get_vertical_monotone()
    monotone_order = transport_metadata%get_vertical_monotone_order()
    reversibility = transport_metadata%get_reversible()
    log_space  = transport_metadata%get_log_space()
    step = transport_counter%get_split_step_of_substep_counter()
    splitting = transport_metadata%get_splitting()

    if ( monotone /= monotone_none    .and. &
         monotone /= monotone_strict  .and. &
         monotone /= monotone_relaxed       ) then
      call log_event( "vertical_sl_advective: Invalid monotone", LOG_LEVEL_ERROR )
    end if
    if ( monotone_order /= vertical_monotone_order_constant  .and. &
         monotone_order /= vertical_monotone_order_linear    .and. &
         monotone_order /= vertical_monotone_order_high            ) then
      call log_event( "vertical_sl_advective: Invalid monotone_order", LOG_LEVEL_ERROR )
    end if

    sl_order = vertical_sl_order
    if ( reversibility ) then
      sl_order = vertical_sl_order_cubic_hermite
    end if

    ! Compute SL advective transport (vertical only) of
    ! field_np1 =  field_n_D
    call invoke( setval_X(field_np1, field_n) )

    ! Get the SL interpolation coefficients and indices
    linear_coeffs => wind_precomputations%get_vert_sl_coeff(                   &
        mesh_id, space, vertical_sl_order_linear, splitting, step              &
    )
    interp_coeffs => wind_precomputations%get_vert_sl_coeff(                   &
        mesh_id, space, sl_order, splitting, step                              &
    )
    interp_indices => wind_precomputations%get_vert_sl_index(                  &
        mesh_id, space, sl_order, splitting, step                              &
    )

    if (sl_order == vertical_sl_order_quintic) then
      ! Quintic semi-Lagrangian transport requires extra interpolation values
      call invoke( vertical_quintic_sl_kernel_type( field_np1,                 &
                                                    interp_coeffs(1),          &
                                                    interp_coeffs(2),          &
                                                    interp_coeffs(3),          &
                                                    interp_coeffs(4),          &
                                                    interp_coeffs(5),          &
                                                    interp_coeffs(6),          &
                                                    interp_indices(1),         &
                                                    interp_indices(2),         &
                                                    interp_indices(3),         &
                                                    interp_indices(4),         &
                                                    interp_indices(5),         &
                                                    interp_indices(6),         &
                                                    linear_coeffs(1),          &
                                                    linear_coeffs(2),          &
                                                    monotone,                  &
                                                    monotone_order,            &
                                                    log_space ) )
    else
      ! Cubic (Lagrange or Hermite) semi-Lagrangian transport
      call invoke( vertical_cubic_sl_kernel_type( field_np1,                   &
                                                  interp_coeffs(1),            &
                                                  interp_coeffs(2),            &
                                                  interp_coeffs(3),            &
                                                  interp_coeffs(4),            &
                                                  interp_indices(1),           &
                                                  interp_indices(2),           &
                                                  interp_indices(3),           &
                                                  interp_indices(4),           &
                                                  linear_coeffs(1),            &
                                                  linear_coeffs(2),            &
                                                  monotone,                    &
                                                  monotone_order,              &
                                                  log_space ) )
    end if

    ! End of step: if necessary enforce min val and overwrite in blending zone
    call end_of_advective_step_alg(                                            &
            field_np1, field_n, transport_counter, transport_metadata          &
    )

    if ( LPROF ) call stop_timing( id, 'transport.sl_vertical' )

  end subroutine vertical_sl_advective_alg

end module vertical_sl_advective_alg_mod
