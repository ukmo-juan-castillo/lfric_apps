!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Integrate a W3 field as though it were a finite difference
!>        representation
module sum_fluxes_alg_mod

  use sci_multi_extract_kernel_mod, only: multi_extract_kernel_type

  implicit none

  private
  public :: sum_fluxes_alg

contains

  subroutine sum_fluxes_alg( accumulated_fluxes, radiation_fields, turbulence_fields, &
                             convection_fields, microphysics_fields )

    use constants_mod,              only: r_def
    use driver_water_constants_mod, only: Lc => latent_heat_h2o_condensation, &
                                          Lf => latent_heat_h2o_fusion
    use field_mod,                  only: field_type
    use field_collection_mod,       only: field_collection_type

    implicit none

    type( field_type ), intent(inout)         :: accumulated_fluxes
    type( field_collection_type ), intent(in) :: radiation_fields
    type( field_collection_type ), intent(in) :: turbulence_fields
    type( field_collection_type ), intent(in) :: convection_fields
    type( field_collection_type ), intent(in) :: microphysics_fields

    real( kind=r_def ) :: Lcf

    type( field_type ), pointer :: heat_flux_bl => null()
    type( field_type ), pointer :: sw_direct_toa_rts => null()
    type( field_type ), pointer :: sw_up_toa_rts => null()
    type( field_type ), pointer :: lw_up_toa_rts => null()
    type( field_type ), pointer :: sw_down_surf_rts => null()
    type( field_type ), pointer :: sw_up_surf_rts => null()
    type( field_type ), pointer :: lw_down_surf_rts => null()
    type( field_type ), pointer :: lw_up_surf_rts => null()
    type( field_type ), pointer :: ls_rain => null()
    type( field_type ), pointer :: ls_snow => null()
    type( field_type ), pointer :: conv_rain => null()
    type( field_type ), pointer :: conv_snow => null()

    type( field_type )          :: heat_flux_surf

    Lcf = Lc + Lf

    call turbulence_fields%get_field('heat_flux_bl', heat_flux_bl)
    call radiation_fields%get_field('sw_direct_toa_rts', sw_direct_toa_rts)
    call radiation_fields%get_field('sw_up_toa_rts', sw_up_toa_rts)
    call radiation_fields%get_field('lw_up_toa_rts', lw_up_toa_rts)
    call radiation_fields%get_field('sw_down_surf_rts', sw_down_surf_rts)
    call radiation_fields%get_field('sw_up_surf_rts', sw_up_surf_rts)
    call radiation_fields%get_field('lw_down_surf_rts', lw_down_surf_rts)
    call radiation_fields%get_field('lw_up_surf_rts', lw_up_surf_rts)
    call microphysics_fields%get_field('ls_rain', ls_rain)
    call microphysics_fields%get_field('ls_snow', ls_snow)
    call convection_fields%get_field('conv_rain', conv_rain)
    call convection_fields%get_field('conv_snow', conv_snow)

    call ls_rain%copy_field_properties(heat_flux_surf)

    call invoke( multi_extract_kernel_type(heat_flux_surf, heat_flux_bl, 1, 1), &
                 inc_X_plus_Y(accumulated_fluxes, heat_flux_surf),              &
                 inc_X_plus_Y(accumulated_fluxes, sw_direct_toa_rts),           &
                 inc_X_minus_Y(accumulated_fluxes, sw_up_toa_rts),              &
                 inc_X_minus_Y(accumulated_fluxes, lw_up_toa_rts),              &
                 inc_X_minus_Y(accumulated_fluxes, sw_down_surf_rts),           &
                 inc_X_plus_Y(accumulated_fluxes, sw_up_surf_rts),              &
                 inc_X_minus_Y(accumulated_fluxes, lw_down_surf_rts),           &
                 inc_X_plus_Y(accumulated_fluxes, lw_up_surf_rts),              &
                 inc_X_plus_bY(accumulated_fluxes, Lc, ls_rain),                &
                 inc_X_plus_bY(accumulated_fluxes, Lcf, ls_snow),               &
                 inc_X_plus_bY(accumulated_fluxes, Lc, conv_rain),              &
                 inc_X_plus_bY(accumulated_fluxes, Lcf, conv_snow) )

    nullify(heat_flux_bl, sw_direct_toa_rts, sw_up_toa_rts, lw_up_toa_rts)
    nullify(sw_down_surf_rts, sw_up_surf_rts, lw_down_surf_rts)
    nullify(lw_up_surf_rts, ls_rain, ls_snow, conv_rain, conv_snow)

  end subroutine sum_fluxes_alg

end module sum_fluxes_alg_mod
