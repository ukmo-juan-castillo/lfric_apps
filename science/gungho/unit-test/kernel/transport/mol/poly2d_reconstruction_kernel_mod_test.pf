!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the polynomial reconstruction of a tracer field on horizontal
!> edges (valid for lowest order quadrilateral elements)
module poly2d_reconstruction_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: poly2d_reconstruction_test_type
    private
  contains
    procedure test_all
  end type poly2d_reconstruction_test_type

contains

  @Test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env, only: real64
    use poly2d_reconstruction_kernel_mod, only: poly2d_reconstruction_code

    implicit none

    class(poly2d_reconstruction_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    real(r_def)            :: use_tol
    real(r_def)            :: answer

    integer(i_def), parameter                      :: nlayers = 3
    integer(i_def), parameter                      :: nfaces_h = 4
    integer(i_def), parameter                      :: stencil_size = 3
    integer(i_def), parameter                      :: ndata = stencil_size*nfaces_h
    integer(i_def), parameter                      :: ndf_md = 2
    integer(i_def), parameter                      :: ndf_wt = 2
    integer(i_def), parameter                      :: ndf_c  = 1
    integer(i_def), parameter                      :: undf_md = nfaces_h*(nlayers+1)
    integer(i_def), parameter                      :: undf_c  = ndata
    integer(i_def), dimension(ndf_md)              :: map_md
    integer(i_def), dimension(ndf_c)               :: map_c
    integer(i_def), dimension(ndf_wt,stencil_size) :: stencil_map
    integer(i_def), parameter                      :: undf_wt = (nlayers+1)*stencil_size

    real(r_def),    dimension(undf_md) :: reconstruction
    real(r_def),    dimension(undf_wt) :: tracer
    real(r_def),    dimension(undf_c)  :: coeff

    integer(i_def) :: i, f1, f2

    do i = 1,stencil_size
      stencil_map(1,i) = 1 + (i-1)*(nlayers+1)
      stencil_map(2,i) = 2 + (i-1)*(nlayers+1)
    end do
    map_c(:) = (/ 1 /)
    map_md(:) = (/ 1, 2 /)

    ! Set up coeff field for quadratic fit in x direction
    coeff(:) = 0.0_r_def
    do i = 0, nfaces_h-1
      if ( i == 0 .or. i == 2 ) then
        f1 = 2
        f2 = 1
      else
        f1 = 1
        f2 = 2
      end if
      coeff((0 + i*stencil_size) + map_c(1)) =  5.0_r_def/6.0_r_def
      coeff((f1 + i*stencil_size) + map_c(1)) = -1.0_r_def/6.0_r_def
      coeff((f2 + i*stencil_size) + map_c(1)) =  2.0_r_def/6.0_r_def
    end do

    ! Set up tracer field so that d(tracer)/dx = 10
    tracer(:) = 300.0_r_def
    do i = 0,nlayers
      tracer(stencil_map(1,1)+i) = 300.0_r_def
      tracer(stencil_map(1,2)+i) = 290.0_r_def
      tracer(stencil_map(1,3)+i) = 310.0_r_def
    end do
    reconstruction(:) = 0.0_r_def
    call poly2d_reconstruction_code( nlayers,              &
                                     reconstruction,       &
                                     tracer,               &
                                     stencil_size,         &
                                     stencil_map,          &
                                     coeff,                &
                                     ndata,                &
                                     stencil_size,         &
                                     ndf_md,               &
                                     undf_md,              &
                                     map_md,               &
                                     ndf_wt,               &
                                     undf_wt,              &
                                     stencil_map(:,1),     &
                                     ndf_c,                &
                                     undf_c,               &
                                     map_c )
    if ( r_def == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_def*spacing(maxval(reconstruction(1:nlayers+1)))
    end if

    answer = 295.0_r_def
    do i = 0,nlayers
      @assertEqual(answer, reconstruction(map_md(1)+i) , use_tol)
    end do
    answer = 305.0_r_def
    do i = 0,nlayers
      @assertEqual(answer, reconstruction(map_md(1)+i+1*(nlayers+1)) , use_tol)
    end do

  end subroutine test_all

end module poly2d_reconstruction_kernel_mod_test
