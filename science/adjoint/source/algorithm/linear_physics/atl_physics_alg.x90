!-----------------------------------------------------------------------------
! (c) Crown copyright 2026 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be brief.
!-----------------------------------------------------------------------------
!> @brief Wrapper for adjoint physics code
module atl_physics_alg_mod

  use constants_mod,                       only: r_def
  use driver_modeldb_mod,                  only: modeldb_type
  use field_mod,                           only: field_type
  use derived_config_mod,                  only: bundle_size
  use field_indices_mod,                   only: igh_u
  use timing_mod,                          only: start_timing, stop_timing, tik, LPROF
  use mesh_mod,                            only: mesh_type
  use fs_continuity_mod,                   only: W2
  use sci_geometric_constants_mod,         only: get_da_at_w2
  use sci_fem_constants_mod,               only: get_mass_matrix_fe
  use sci_enforce_bc_kernel_mod,           only: enforce_bc_kernel_type
  use adj_matrix_vector_kernel_mod,        only: adj_matrix_vector_kernel_type
  use atl_bdy_lyr_alg_mod,                 only: atl_bdy_lyr_alg
  use sci_mass_matrix_solver_alg_mod,      only: mass_matrix_solver_alg
  use sci_field_bundle_builtins_mod,       only: clone_bundle, copy_bundle, set_bundle_scalar
  use operator_mod,                        only: operator_type

  implicit none

  private
  public :: atl_physics_alg

contains

!> @brief Wrapper for adjoint physics code, currently just atl_bdy_lyr_alg
!> @param[in,out] modeldb    Structure containing the model state
!> @param[in,out] u          The TL model u field to be incremented
!> @param[in,out] state_star TL model prognostic fields for physics calculations
!> @param[in,out] rhs_phys   Residuals
!> @param[in]     state      The current TL model prognostic fields
!> @param[in]     rhs_n      Residuals
!> @param[in]     rhs_np1    Residuals
!> @param[in]     rhs_adv    Advective terms
!> @param[in]     ls_state   Lin state for Prognostic model state
!> @param[in]     mesh       The current mesh
!> @param[in]     dt         The TL model timestep length
subroutine atl_physics_alg(modeldb,    &
                           u,          &
                           state_star, &
                           rhs_phys,   &
                           rhs_np1,    &
                           rhs_n,      &
                           state,      &
                           rhs_adv,    &
                           ls_state,   &
                           mesh,       &
                           dt)

  implicit none

  type(modeldb_type), target,  intent(inout) :: modeldb
  type(field_type),            intent(inout) :: u
  type(field_type),            intent(inout) :: state_star(bundle_size)
  type(field_type),            intent(inout) :: rhs_phys(bundle_size)
  type(field_type),            intent(in)    :: state(bundle_size)
  type(field_type),            intent(in)    :: rhs_n(bundle_size)
  type(field_type),            intent(in)    :: rhs_np1(bundle_size)
  type(field_type),            intent(in)    :: rhs_adv(bundle_size)
  type(field_type),            intent(in)    :: ls_state(bundle_size)
  type(mesh_type),    pointer, intent(in)    :: mesh
  real(kind=r_def),            intent(in)    :: dt

  type(field_type) :: u_bl_inc
  type(field_type) :: u_bl_inc_flux
  type(field_type) :: du
  type(field_type) :: u_star
  type(field_type) :: u_star_physical
  type(field_type) :: rhsu_np1

  type(operator_type), pointer :: mm_vel
  type(field_type),    pointer :: dA

  integer(kind=tik) :: id

  if (LPROF) call start_timing(id, 'atl_physics_alg')

  mm_vel => get_mass_matrix_fe(W2, mesh%get_id())

  call u%copy_field_properties( u_bl_inc )
  call u%copy_field_properties( u_bl_inc_flux )
  call u%copy_field_properties( du )
  call u%copy_field_properties( u_star )
  call u%copy_field_properties( u_star_physical )
  call rhs_adv(igh_u)%copy_field_properties( rhsu_np1 )

  call du%initialise( rhs_adv(igh_u)%get_function_space() )
  call rhsu_np1%initialise( rhs_adv(igh_u)%get_function_space() )

  call clone_bundle( state, state_star, bundle_size )
  call copy_bundle( state, state_star, bundle_size ) ! Only state_star(igh_u) is used
  call set_bundle_scalar( 0.0_r_def, state_star, bundle_size )

  dA => get_da_at_w2(mesh%get_id())

  call invoke( setval_c( u_bl_inc_flux, 0.0_r_def ),   &
               setval_c( u_bl_inc, 0.0_r_def ),        &
               setval_c( u_star, 0.0_r_def ),          &
               setval_c( u_star_physical, 0.0_r_def ), &
               setval_c( du, 0.0_r_def ),              &
               setval_c( rhsu_np1, 0.0_r_def ) )

  call invoke( enforce_bc_kernel_type( rhs_phys(igh_u) ), &
               adj_matrix_vector_kernel_type( rhs_phys(igh_u), u_bl_inc_flux, mm_vel ) )

  call set_bundle_scalar( 0.0_r_def, rhs_phys, bundle_size )

  ! Adj of u_bl_inc_flux = u_bl_inc * dA
  call invoke( inc_x_times_y( u_bl_inc_flux, dA ),      &
               inc_x_plus_y( u_bl_inc, u_bl_inc_flux ) )

  call atl_bdy_lyr_alg( modeldb, u_bl_inc, state_star(igh_u), &
                        ls_state, dt )

  ! Adj of state_star(igh_u) <- u_star_physical
  call invoke( inc_x_plus_y( u_star_physical, state_star(igh_u) ), &
               setval_c( state_star(igh_u), 0.0_r_def ) )

  ! Adj of u_star_physical = u_star / dA
  call invoke( inc_x_divideby_y( u_star_physical, dA ), &
               inc_x_plus_y( u_star, u_star_physical ) )

  ! Adj of u_star = du + state(igh_u)
  call invoke( inc_X_plus_Y( du, u_star ), &
               inc_X_plus_Y( state(igh_u), u_star ) )

  ! Adj of call mass_matrix_solver_alg(du, rhsu_np1)
  call mass_matrix_solver_alg( rhsu_np1, du )

  ! Adj of inc_X_plus_Y(rhsu_np1, rhs_adv(igh_u))
  call invoke( inc_X_plus_Y( rhs_adv(igh_u), rhsu_np1 ) )

  ! Adj of rhsu_np1 <- -rhs_np1(igh_u) + rhs_n(igh_u)
  call invoke( inc_X_plus_bY( rhs_np1(igh_u), -1.0_r_def, rhsu_np1 ), &
               inc_X_plus_Y( rhs_n(igh_u), rhsu_np1 ) )

  if (LPROF) call stop_timing(id, 'atl_physics_alg')

end subroutine atl_physics_alg

end module atl_physics_alg_mod
