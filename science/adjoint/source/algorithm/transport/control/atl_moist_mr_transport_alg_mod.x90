!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Routine for adjoint transport of the moisture mixing ratios.

module atl_moist_mr_transport_alg_mod

  use constants_mod,                  only: i_def, r_def
  use field_mod,                      only: field_type
  use log_mod,                        only: log_event,       &
                                            LOG_LEVEL_ERROR
  use mr_indices_mod,                 only: nummr
  use timing_mod,                     only: start_timing, stop_timing, &
                                            tik, LPROF
  use tl_transport_controller_mod,    only: tl_transport_controller_type
  use transport_enumerated_types_mod, only: equation_form_advective, &
                                            equation_form_conservative
  use atl_transport_field_mod,        only: atl_transport_field
  use transport_metadata_mod,         only: transport_metadata_type

  implicit none

  private

  public :: atl_moist_mr_transport_alg

contains

  !=============================================================================
  !> @brief Central routine for adjoint transport of moisture fields.
  !> @param[in,out] mr_out     ACTIVE  Perturbation to moisture mixing ratios
  !> @param[in,out] mr_in      ACTIVE  Input perturbation state for moisture
  !> @param[in]     ls_mr_in   PASSIVE Lin. state for moisture mixing ratios
  !> @param[in]     nummr_to_transport Number of moisture species to transport
  !> @param[in,out] tl_transport_controller
  !!                           Object controlling transport
  !> @param[in,out] transport_metadata
  !!                           Contains the configuration options for
  !!                           transporting these fields
  subroutine atl_moist_mr_transport_alg( mr_out, mr_in, ls_mr_in, &
                                         nummr_to_transport,      &
                                         tl_transport_controller, &
                                         transport_metadata )

    implicit none

    ! Arguments
    type(field_type),                   intent(inout) :: mr_out(nummr)
    type(field_type),                   intent(inout) :: mr_in(nummr)
    type(field_type),                      intent(in) :: ls_mr_in(nummr)
    integer(kind=i_def),                   intent(in) :: nummr_to_transport
    type(tl_transport_controller_type), intent(inout) :: tl_transport_controller
    type(transport_metadata_type),      intent(inout) :: transport_metadata

    ! Internal variables
    integer(kind=i_def) :: imr
    integer(tik)        :: id

    if ( LPROF ) call start_timing( id, 'atl_mois_mixing_ratio_transport' )

    ! ------------------------------------------------------------------------ !
    ! Copy non-transported fields over
    ! ------------------------------------------------------------------------ !

    do imr = nummr, nummr_to_transport + 1, -1
      call invoke( inc_X_plus_Y( mr_in(imr), mr_out(imr) ), &
                   setval_c( mr_out(imr), 0.0_r_def ) )
    end do

    ! Choose form of transport equation
    select case ( transport_metadata%get_equation_form() )

    ! ------------------------------------------------------------------------ !
    ! Advective form of transport equation
    ! ------------------------------------------------------------------------ !
    case ( equation_form_advective )

      ! Simply transport all the mixing ratio fields as they are in Wtheta
      do imr = nummr_to_transport, 1, -1
        call atl_transport_field( mr_out(imr), mr_in(imr), ls_mr_in(imr), &
                                  tl_transport_controller, transport_metadata )
      end do

    ! ------------------------------------------------------------------------ !
    ! Conservative form of transport equation
    ! ------------------------------------------------------------------------ !
    case ( equation_form_conservative )
      call log_event('Conservative moisture transport equation not ' // &
                     'yet implemented', LOG_LEVEL_ERROR)

    ! ------------------------------------------------------------------------ !
    ! Default form of transport equation
    ! ------------------------------------------------------------------------ !
    case default
      call log_event('Form of moisture transport equation either not ' // &
                     'compatible or not implemented', LOG_LEVEL_ERROR)

    end select

    if ( LPROF ) call stop_timing( id, 'atl_mois_mixing_ratio_transport' )

  end subroutine atl_moist_mr_transport_alg

end module atl_moist_mr_transport_alg_mod
