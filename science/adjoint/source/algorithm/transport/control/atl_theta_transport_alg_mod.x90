!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Adjoint routine for transport of potential temperature.

module atl_theta_transport_alg_mod

  use constants_mod,                  only: r_def
  use field_mod,                      only: field_type
  use log_mod,                        only: log_event,       &
                                            LOG_LEVEL_ERROR
  use atl_transport_field_mod,        only: atl_transport_field
  use transport_config_mod,           only: theta_variable,             &
                                            theta_variable_dry,         &
                                            adjust_theta
  use tl_transport_controller_mod,    only: tl_transport_controller_type
  use adj_trans_lookup_cache_mod,     only: adj_trans_lookup_cache_type
  use transport_enumerated_types_mod, only: equation_form_advective,    &
                                            equation_form_consistent
  use transport_metadata_mod,         only: transport_metadata_type

  implicit none

  private

  public :: atl_theta_transport_alg

contains

  !=============================================================================
  !> @brief Adjoint routine for transporting potential temperature field.
  !> @param[in,out] theta_out                Perturbation: Dry potential temperature
  !!                                         after transport
  !> @param[in,out] theta_inc                Perturbation: Dry potential temperature
  !!                                         increment
  !> @param[in,out] theta_in                 Perturbation: Dry potential temperature
  !!                                         before transport
  !> @param[in]     ls_theta_in              Linear state: Dry potential temperature
  !!                                         before transport
  !> @param[in,out] tl_transport_controller  Controls transport
  !> @param[in,out] transport_metadata       Contains the configuration options for
  !!                                         transporting the potential temperature
  !> @param[in]     adj_lookup_table_cache   Lookup table cache
  subroutine atl_theta_transport_alg( theta_out, theta_inc,                        &
                                      theta_in, ls_theta_in,                       &
                                      tl_transport_controller, transport_metadata, &
                                      adj_lookup_table_cache )

    implicit none

    ! Arguments
    type(field_type),           target, intent(inout) :: theta_out
    type(field_type),                   intent(inout) :: theta_inc
    type(field_type),           target, intent(inout) :: theta_in
    type(field_type),           target, intent(in)    :: ls_theta_in
    type(tl_transport_controller_type), intent(inout) :: tl_transport_controller
    type(transport_metadata_type),      intent(inout) :: transport_metadata
    type(adj_trans_lookup_cache_type),  intent(in)    :: adj_lookup_table_cache

    ! Internal variables
    real(kind=r_def)                      :: one
    type(field_type),             pointer :: theta_in_ptr
    type(field_type),             pointer :: theta_out_ptr
    type(field_type),             pointer :: ls_theta_in_ptr

    ! ------------------------------------------------------------------------ !
    ! Convert to potential temperature to transport
    ! ------------------------------------------------------------------------ !

    select case ( theta_variable )

      case ( theta_variable_dry )
        ! Already have the correct variable, so just point to it
        theta_in_ptr => theta_in
        theta_out_ptr => theta_out
        ls_theta_in_ptr => ls_theta_in

      case default
        call log_event('A.T.L. Theta variable not implemented', LOG_LEVEL_ERROR)

    end select

    ! ------------------------------------------------------------------------ !
    ! Compute increment
    ! ------------------------------------------------------------------------ !

    call invoke( inc_X_minus_Y( theta_in, theta_inc ), &
                 inc_X_plus_Y( theta_out, theta_inc ), &
                 setval_c( theta_inc, 0.0_r_def ) )

    ! ------------------------------------------------------------------------ !
    ! Apply static adjustment if desired
    ! ------------------------------------------------------------------------ !

    if ( adjust_theta ) then
      call log_event('A.T.L. Theta adjustment not implemented', LOG_LEVEL_ERROR)
    end if

    ! ------------------------------------------------------------------------ !
    ! Return to dry theta
    ! ------------------------------------------------------------------------ !

    select case ( theta_variable )

      case ( theta_variable_dry )
        ! Nothing to do -- can't "pass" in Fortran so just set a dummy variable
        one = 1.0_r_def

      case default
        call log_event('A.T.L. Theta variable not implemented', LOG_LEVEL_ERROR)

    end select

    ! ------------------------------------------------------------------------ !
    ! Transport potential temperature depending on equation
    ! ------------------------------------------------------------------------ !
    select case ( transport_metadata%get_equation_form() )

      ! ---------------------------------------------------------------------- !
      ! Advective and consistent forms of transport equation
      ! ---------------------------------------------------------------------- !
      case ( equation_form_advective, equation_form_consistent )

        ! Advective:  Simply transport all the potential temperature in Wtheta
        ! Consistent: Transformation to densities and evaluation of fluxes is in
        !             lowest level algorithms, so just call transport_field
        call atl_transport_field( theta_out_ptr, theta_in_ptr, ls_theta_in_ptr, &
                                  tl_transport_controller, transport_metadata,  &
                                  adj_lookup_table_cache )

    ! ------------------------------------------------------------------------ !
    ! Default form of transport equation
    ! ------------------------------------------------------------------------ !
    case default
      call log_event('A.T.L. Form of moisture transport equation either not ' // &
                     'compatible or not implemented', LOG_LEVEL_ERROR)

    end select

  end subroutine atl_theta_transport_alg

end module atl_theta_transport_alg_mod
