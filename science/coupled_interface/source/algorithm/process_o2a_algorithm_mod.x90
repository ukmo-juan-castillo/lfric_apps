!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Process ocean and sea ice data coming through the coupler

module process_o2a_algorithm_mod
  use constants_mod,                 only: i_def, r_def
  use coupler_external_field_mod,    only: set_r_sea_ice_frac_raw
  use field_mod,                     only: field_type
  use field_collection_mod,          only: field_collection_type
  use integer_field_mod,             only: integer_field_type
  use process_o2a_kernel_mod,        only: process_o2a_kernel_type
  use geometric_constants_mod,       only: get_da_at_w2
  use map_fd_to_prognostics_alg_mod, only: set_wind
  use log_mod,                       only: log_event, LOG_LEVEL_INFO
  use mesh_mod,                      only: mesh_type
  use sci_multi_insert_kernel_mod,   only: multi_insert_kernel_type

  implicit none

  public process_o2a_algorithm

  contains

  !> @brief Process ocean and sea ice data coming through the coupler
  !> @param[in] dcpl_rcv field collection of all fields recieved through the coupler
  !> @param[in] depository field collection of all other relevant fields
  !> @param[in] n_sea_ice_tile Number of sea ice tiles
  subroutine process_o2a_algorithm(dcpl_rcv,depository,              &
                                   n_sea_ice_tile, T_freeze_h2o_sea, &
                                   therm_cond_sice, therm_cond_sice_snow)
   implicit none
   type( field_collection_type ), intent(in)    :: dcpl_rcv
   type( field_collection_type ), intent(in)    :: depository
   integer(kind=i_def),           intent(in)    :: n_sea_ice_tile
   real(kind=r_def),              intent(in)    :: T_freeze_h2o_sea
   real(kind=r_def),              intent(in)    :: therm_cond_sice
   real(kind=r_def),              intent(in)    :: therm_cond_sice_snow

   !local variables
   type( field_type ), pointer        :: sea_surf_temp_ptr => null()
   type( field_type ), pointer        :: sea_ice_fraction_ptr => null()
   type( field_type ), pointer        :: sea_ice_thickness_ptr => null()
   type( field_type ), pointer        :: sea_ice_layer_t_ptr => null()
   type( field_type ), pointer        :: sea_ice_conductivity_ptr => null()
   type( field_type ), pointer        :: sea_ice_snow_depth_ptr => null()
   type( field_type ), pointer        :: melt_pond_fraction_ptr => null()
   type( field_type ), pointer        :: melt_pond_depth_ptr => null()
   type( field_type ), pointer        :: sea_u_current_ptr => null()
   type( field_type ), pointer        :: sea_v_current_ptr => null()
   type( field_type ), pointer        :: sea_u_w3_ptr => null()
   type( field_type ), pointer        :: sea_v_w3_ptr => null()
   type( field_type ), pointer        :: sea_w_w3_ptr => null()
   type( field_type ), pointer        :: sea_current_w2_ptr => null()
   type( field_type ), pointer        :: dA => null()
   type( field_type ), pointer        :: theta_ptr => null()
   type( integer_field_type ), pointer        :: ocn_cpl_point_ptr => null()
   type(mesh_type), pointer :: mesh => null()

   ! Get the pointers to the fields
   call dcpl_rcv%get_field("lf_ocn_sst", sea_surf_temp_ptr)
   call dcpl_rcv%get_field("lf_icefrc", sea_ice_fraction_ptr)
   call dcpl_rcv%get_field("lf_icetck", sea_ice_thickness_ptr)
   call dcpl_rcv%get_field("lf_icelayert", sea_ice_layer_t_ptr)
   call dcpl_rcv%get_field("lf_conductivity", sea_ice_conductivity_ptr)
   call dcpl_rcv%get_field("lf_snow_depth", sea_ice_snow_depth_ptr)
   call dcpl_rcv%get_field("lf_pond_frac", melt_pond_fraction_ptr)
   call dcpl_rcv%get_field("lf_pond_depth", melt_pond_depth_ptr)
   call dcpl_rcv%get_field("lf_sunocean",sea_u_current_ptr)
   call dcpl_rcv%get_field("lf_svnocean",sea_v_current_ptr)
   call depository%get_field("sea_u_3d",sea_u_w3_ptr)
   call depository%get_field("sea_v_3d",sea_v_w3_ptr)
   call depository%get_field("sea_w_3d",sea_w_w3_ptr)
   call depository%get_field("sea_current_w2",sea_current_w2_ptr)
   call depository%get_field("theta",theta_ptr)
   call depository%get_field("ocn_cpl_point",ocn_cpl_point_ptr)

   mesh => theta_ptr%get_mesh()
   dA => get_da_at_w2( mesh%get_id() )

   ! Prepare to convert W3 ocean velocities to W2 by initialising our
   ! various work arrays to zero.
   call invoke(setval_c(sea_u_w3_ptr, 0.0_r_def ),     &
               setval_c(sea_v_w3_ptr, 0.0_r_def ),     &
               setval_c(sea_w_w3_ptr, 0.0_r_def ),     &
               setval_c(sea_current_w2_ptr, 0.0_r_def) &
              ) ! Close invoke

   ! Move our incoming U and V components to the bottom
   ! level of 3d equivalent fields
   call invoke(multi_insert_kernel_type(sea_u_w3_ptr, sea_u_current_ptr, 1, 1), &
               multi_insert_kernel_type(sea_v_w3_ptr, sea_v_current_ptr, 1, 1)  &
              )

   ! Convert U/V (and W) from W3 to W2
   call set_wind( sea_current_w2_ptr, sea_u_w3_ptr, sea_v_w3_ptr, sea_w_w3_ptr )

   ! set_wind produces values in terms of fluxes, which are not appropriate
   ! for direct use in the things we need (JULES). We must convert the resulting
   ! values to what LFRic code refers to as "physics values".
   ! i.e. not fluxes, but simply m/s.
   call invoke(inc_X_divideby_Y(sea_current_w2_ptr, dA))

   ! Save raw sea ice fractions before any post processing
   ! for use in scaling data going from atmos to ocean
   call set_r_sea_ice_frac_raw(sea_ice_fraction_ptr)

   ! Process data that has just come through the coupler
   call invoke(process_o2a_kernel_type( sea_surf_temp_ptr,        &
                                        sea_ice_fraction_ptr,     &
                                        sea_ice_thickness_ptr,    &
                                        sea_ice_layer_t_ptr,      &
                                        sea_ice_conductivity_ptr, &
                                        sea_ice_snow_depth_ptr,   &
                                        melt_pond_fraction_ptr,   &
                                        melt_pond_depth_ptr,      &
                                        ocn_cpl_point_ptr,        &
                                        n_sea_ice_tile,           &
                                        T_freeze_h2o_sea,         &
                                        therm_cond_sice,          &
                                        therm_cond_sice_snow      &
                                      )                           &
              )   ! Close invoke.

  end subroutine process_o2a_algorithm

end module process_o2a_algorithm_mod
