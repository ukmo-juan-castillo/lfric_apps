!-----------------------------------------------------------------------------
! (c) Crown copyright 2024 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Process ocean and sea ice data coming through the coupler

module process_recv_fields_2d_mod
  use constants_mod,                 only: i_def, r_def
  use process_send_fields_2d_mod,    only: set_r_sea_ice_frac_raw
  use field_mod,                     only: field_type
  use field_collection_mod,          only: field_collection_type
  use integer_field_mod,             only: integer_field_type
  use process_o2a_kernel_mod,        only: process_o2a_kernel_type
  use sci_geometric_constants_mod,   only: get_da_at_w2
  use map_fd_to_prognostics_alg_mod, only: set_wind
  use log_mod,                       only: log_event, LOG_LEVEL_INFO
  use mesh_mod,                      only: mesh_type
  use sci_multi_insert_kernel_mod,   only: multi_insert_kernel_type
  use derived_config_mod,         only: l_couple_ocean, l_couple_sea_ice

  implicit none

  public process_recv_fields_2d

  contains

  !> @brief Process ocean and sea ice data coming through the coupler
  !> @param[in] dcpl_rcv field collection of all fields recieved through the coupler
  !> @param[in] depository field collection of all other relevant fields
  !> @param[in] n_sea_ice_tile Number of sea ice tiles
  !> @param[in] T_freeze_h2o_sea Temperature at which sea water freezes (K)
  !> @param[in] therm_cond_sice Thermal conductivity of sea-ice (W/m/K)
  !> @param[in] therm_cond_sice_snow Thermal conductivity of snow
  !>                                 on zero layer sea-ice (W/m/K)
  subroutine process_recv_fields_2d(dcpl_rcv,depository,              &
                                    n_sea_ice_tile, T_freeze_h2o_sea, &
                                    therm_cond_sice, therm_cond_sice_snow)
   implicit none
   type( field_collection_type ), intent(in)    :: dcpl_rcv
   type( field_collection_type ), intent(in)    :: depository
   integer(kind=i_def),           intent(in)    :: n_sea_ice_tile
   real(kind=r_def),              intent(in)    :: T_freeze_h2o_sea
   real(kind=r_def),              intent(in)    :: therm_cond_sice
   real(kind=r_def),              intent(in)    :: therm_cond_sice_snow

   !local variables
   type( field_type ), pointer        :: sea_surf_temp_ptr
   type( field_type ), pointer        :: sea_ice_fraction_ptr
   type( field_type ), pointer        :: sea_ice_thickness_ptr
   type( field_type ), pointer        :: sea_ice_layer_t_ptr
   type( field_type ), pointer        :: sea_ice_conductivity_ptr
   type( field_type ), pointer        :: sea_ice_snow_depth_ptr
   type( field_type ), pointer        :: melt_pond_fraction_ptr
   type( field_type ), pointer        :: melt_pond_depth_ptr
   type( field_type ), pointer        :: sea_u_current_ptr
   type( field_type ), pointer        :: sea_v_current_ptr
   type( field_type )                 :: sea_u_w3
   type( field_type )                 :: sea_v_w3
   type( field_type )                 :: sea_w_wth
   type( field_type ), pointer        :: sea_current_w2_ptr
   type( field_type ), pointer        :: dA
   type( field_type ), pointer        :: theta_ptr
   type( field_type ), pointer        :: u_w3_ptr
   type( integer_field_type ), pointer        :: ocn_cpl_point_ptr
   type(mesh_type), pointer :: mesh

   ! Get the pointers to the fields
   call dcpl_rcv%get_field("lf_ocn_sst", sea_surf_temp_ptr)
   call dcpl_rcv%get_field("lf_icefrc", sea_ice_fraction_ptr)
   call dcpl_rcv%get_field("lf_icetck", sea_ice_thickness_ptr)
   call dcpl_rcv%get_field("lf_icelayert", sea_ice_layer_t_ptr)
   call dcpl_rcv%get_field("lf_conductivity", sea_ice_conductivity_ptr)
   call dcpl_rcv%get_field("lf_snow_depth", sea_ice_snow_depth_ptr)
   call dcpl_rcv%get_field("lf_pond_frac", melt_pond_fraction_ptr)
   call dcpl_rcv%get_field("lf_pond_depth", melt_pond_depth_ptr)
   call dcpl_rcv%get_field("lf_sunocean",sea_u_current_ptr)
   call dcpl_rcv%get_field("lf_svnocean",sea_v_current_ptr)
   call depository%get_field("sea_current_w2",sea_current_w2_ptr)
   call depository%get_field("theta",theta_ptr)
   call depository%get_field("u_in_w3",u_w3_ptr)
   call depository%get_field("ocn_cpl_point",ocn_cpl_point_ptr)

   mesh => theta_ptr%get_mesh()
   dA => get_da_at_w2( mesh%get_id() )
   call u_w3_ptr%copy_field_properties(sea_u_w3)
   call u_w3_ptr%copy_field_properties(sea_v_w3)
   call theta_ptr%copy_field_properties(sea_w_wth)

   if (l_couple_ocean) then
     ! Prepare to convert W3 ocean velocities to W2 by initialising our
     ! various work arrays to zero.
     call invoke(setval_c(sea_u_w3,  0.0_r_def ),        &
                 setval_c(sea_v_w3,  0.0_r_def ),        &
                 setval_c(sea_w_wth, 0.0_r_def ),        &
                 setval_c(sea_current_w2_ptr, 0.0_r_def) &
                ) ! Close invoke

     ! Move our incoming U and V components to the bottom
     ! level of 3d equivalent fields
     call invoke(multi_insert_kernel_type(sea_u_w3, sea_u_current_ptr, 1, 1), &
                 multi_insert_kernel_type(sea_v_w3, sea_v_current_ptr, 1, 1)  &
                )

     ! Convert U/V (and W) from W3 to W2
     call set_wind( sea_current_w2_ptr, sea_u_w3, sea_v_w3, sea_w_wth )
     call sea_u_w3%field_final()
     call sea_v_w3%field_final()
     call sea_w_wth%field_final()

     ! set_wind produces values in terms of fluxes, which are not appropriate
     ! for direct use in the things we need (JULES). We must convert the resulting
     ! values to what LFRic code refers to as "physics values".
     ! i.e. not fluxes, but simply m/s.
     call invoke(inc_X_divideby_Y(sea_current_w2_ptr, dA))
   end if

   ! Save raw sea ice fractions before any post processing
   ! for use in scaling data going from atmos to ocean
   if (l_couple_sea_ice) then
     call set_r_sea_ice_frac_raw(depository, sea_ice_fraction_ptr)
   end if

   ! Process data that has just come through the coupler
   call invoke(process_o2a_kernel_type( sea_surf_temp_ptr,        &
                                        sea_ice_fraction_ptr,     &
                                        sea_ice_thickness_ptr,    &
                                        sea_ice_layer_t_ptr,      &
                                        sea_ice_conductivity_ptr, &
                                        sea_ice_snow_depth_ptr,   &
                                        melt_pond_fraction_ptr,   &
                                        melt_pond_depth_ptr,      &
                                        ocn_cpl_point_ptr,        &
                                        n_sea_ice_tile,           &
                                        T_freeze_h2o_sea,         &
                                        therm_cond_sice,          &
                                        therm_cond_sice_snow      &
                                      )                           &
              )   ! Close invoke.

  end subroutine process_recv_fields_2d

end module process_recv_fields_2d_mod
